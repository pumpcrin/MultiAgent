version "2019.0"

# info:     static model generation

@Hidden
attr Asset_File = ""
@Hidden
attr Pos_Center = false
@Hidden
attr Pos_Bottom_Align = false  
@Hidden
attr Pos_zUp = false
@Hidden
attr Material_Colorize = "#ffffff"
@Hidden
@Range("keep original", "semitransparent", "opaque")
attr Material_Transparency = "keep original"
@Hidden
@Range("keep original", "soft", "hard", "auto")
attr Normals = "keep original"
@Hidden
@Range("keep original", "cleanup", "reduce 50%", "reduce 90%")
attr Mesh = "keep original"

upMode = case Pos_zUp: "zUp" else: "yUp"

const modifyTransparency = Material_Transparency != "keep original"
const opacityValue = case Material_Transparency == "opaque": 1 else: 0.35

@Hidden
attr scx = 1
@Hidden
attr scy = 1
@Hidden
attr scz = 1
	
@Hidden
StaticModel -->
	set(scx,scope.sx) set(scy,scope.sy) set(scz,scope.sz)
	StaticModelMode

@Hidden
StaticModelMode -->
	case Pos_Center && Pos_Bottom_Align :
		StaticModelCenterAlign
	case Pos_Center:
		StaticModelCenter
	case Pos_Bottom_Align :
		StaticModelAlign
	else:
    	StaticModelOrig

@Hidden
StaticModelOrig -->
	i(Asset_File,upMode,keepSizeAndPosition) s('scx,'scy,'scz)
    set(scope.tx,scx*(scope.tx+0.5))
    set(scope.ty,scy*(scope.ty+0.5))
    set(scope.tz,scz*(scope.tz+scz-0.5))
    StaticModelMesh

@Hidden
StaticModelCenter -->  
	i(Asset_File,upMode,keepSizeAndPosition) s('scx,'scy,'scz) center(xyz) 
	StaticModelMesh

@Hidden
StaticModelAlign -->  
	i(Asset_File,upMode,keepSizeAndPosition) s('scx,'scy,'scz) center(y) t(0,'0.5,0) 
   	set(scope.tx,scx*(scope.tx+0.5)) set(scope.tz,scz*(scope.tz+scz-0.5))
   	StaticModelMesh

@Hidden
StaticModelCenterAlign -->   	
  	i(Asset_File,upMode,keepSizeAndPosition) s('scx,'scy,'scz) center(xyz) t(0,'0.5,0) StaticModelMesh

@Hidden
StaticModelMesh -->
	case Mesh == "cleanup":
		cleanupGeometry(all,0.005)
		StaticModelNormals
	case Mesh == "reduce 50%":
		cleanupGeometry(all,0.005)
		reduceGeometry(0.5)
		StaticModelNormals
	case Mesh == "reduce 90%":
		cleanupGeometry(all,0.005)
		reduceGeometry(0.9)
		StaticModelNormals
	else:
		StaticModelNormals

@Hidden
StaticModelNormals -->
	case Normals == "hard":
		setNormals(hard)
		StaticModelFinal
	case Normals == "soft":
		setNormals(soft)
		StaticModelFinal
	case Normals == "auto":
		setNormals(conform)
		setNormals(auto)
		StaticModelFinal
	else:
		StaticModelFinal
		
@Hidden
StaticModelFinal -->
    case Material_Colorize != "#ffffff" && modifyTransparency:
        set(material.opacity,opacityValue)
        color(Material_Colorize)
    case Material_Colorize != "#ffffff":
        color(Material_Colorize)
    case modifyTransparency:
        set(material.opacity,opacityValue)
    else:
        StaticModelFinal.
